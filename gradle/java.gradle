apply plugin: 'java'
apply plugin: 'maven-publish'
//apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Build Source Jars
def sourcesJarMap = [name: 'sourcesJar', type: Jar, dependsOn: classes]
project.tasks.create(sourcesJarMap, {
    classifier = 'sources'
    from sourceSets.main.allSource
    version = "${project.version}"
})

// Build Javadoc Jars
def javadocJarMap = [name: 'javadocJar', type: Jar, dependsOn: javadoc]
project.tasks.create(javadocJarMap, {
    classifier = 'javadoc'
    from javadoc.destinationDir
    version = "${project.version}"
})

// Build Artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    version = "${project.version}"
}

// Artifacts to Publish
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/gregwhitaker/bintray-publish-example'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'gregwhitaker'
                        name = 'Greg Whitaker'
                        email = 'greg@netifi.com'
                        url = 'https://github.com/gregwhitaker'
                    }
                }

                scm {
                    url = 'https://github.com/gregwhitaker/bintray-publish-example'
                    connection = 'scm:git:git://github.com/gregwhitaker/bintray-publish-example.git'
                    developerConnection = 'scm:git:git@github.com:gregwhitaker/bintray-publish-example.git'
                }
            }
        }
    }
}

//// Sign Published Artifacts
//signing {
//    sign publishing.publications.mavenJava
//}
